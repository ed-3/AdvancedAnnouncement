group = 'me.ed333.mcplugin.advancedann'
version = '1.2.2-release'

allprojects {
    group = this.group
    version = this.version
}

subprojects {
    apply plugin: 'java'

    tasks.withType(Jar) {
        getDestinationDirectory().set(file("$rootDir/artifacts"))
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }

    def targetJavaVersion = 8
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release = targetJavaVersion
        }
        options.encoding = "UTF-8"
    }
}